# JAGS model for estimating gray whale abundance off Granite Canyon

# known cyclical nature of gray whale counts is modeled by the function
# provided in Girondot et al. (2007): Modeling approaches to quantify
# leatherback nesting trends in French Guiana and Suriname. Chelonian
# Conservation and Biology 6(1): 37-46.
#
# The function has the following form:
# 
# M1 <- (1 + (2 * exp(K) - 1) * exp((1/S1) * (P - d))) ^ (-1/exp(K))
# M2 <- (1 + (2 * exp(K) - 1) * exp((1/S2) * (P - d))) ^ (-1/exp(K))
# N <- min + (max - min) * (M1 * M2)
#
# d is the number of days from the beginning of migration season
# S1 < 0 and S2 > 0 define the "fatness" of the function
# K > 0 defines the "flatness" at the peak of the function
# P defines where the peak is relatvie to the range of d min(d) < P < max(d)
# min is "the basal level of the number of whales outside the migration season"
# max > min
#
# I set min = 0. 
#
# N is the number of whales passing through the study area during the 9-hr sampling period.
# 
# Observed counts are assumed to be Poisson distribution with its mean = 
# available whales x sighting probability, 
# where sighting probability is a function of visibility, Beaufort, and observers.
# Ten observers were selected for the number of sightings. All others were pooled 
# as an "other" observer. They are treated as fixed effects with independent intercepts.
#
# This version (v4a1) has year spcific P1, P2, S2, and Max parameters with hyperparameters.
# K and S1 are shared among years.
#

model{

	# Define Richards function - this is the mean of "true" daily abundance
	# The realization is a Poisson deviate. 
	for (y in 1:n.year){    
		# Fix Day 1
		mean.N[1, y] <- 0.0001 # Use small epsilon to avoid log(0) errors if used in log-likelihood
    
		# Fix Last Day
		mean.N[n.days, y] <- 0.0001
	
		for (t in 2:(n.days-1)){
			# year-specific K - results in conversion issues. 2022-11-09 
			# This was fixed by adding hyperparameters. 2024
			M1[t, y] <- (1 + (2 * exp(K) - 1) * exp((1/(-S1)) * (P1[y] - t))) ^ (-1/exp(K))
			M2[t, y] <- (1 + (2 * exp(K) - 1) * exp((1/S2[y]) * (P2[y] - t))) ^ (-1/exp(K))
			
			mean.N[t, y] <- (Max[y]) * (M1[t, y] * M2[t, y]) 
						
		}
	}  

	# observations are from the realized abundance: 
		
	for (y in 1:n.year){
		for (s in 1:n.station[y]){
			for (d in 2:(periods[y,s]+1)){
				# Calculate 'Available Whales' based on effort (watch length)
				# watch.length must be in days (which you have: 0.04 to 0.4)
				whales.available[d,s,y] <- mean.N[day[d,s,y], y] * watch.length[(d-1),s,y]
				
				# Poisson Rate
				lambda[d, s, y] <- whales.available[d,s,y] * obs.prob[d,s,y]
            
				n[d, s, y] ~ dpois(lambda[d,s,y])
			
				log.lkhd[(d-1),s,y] <- logdensity.pois(n[d,s,y], lambda[d,s,y])
								
				# probability is mean + covariate effects and watch.length as an offset
				logit(obs.prob[d,s,y]) <- alpha[obs.fixed[d,s,y]] +
								(BF.Fixed * bf[(d-1),s,y]) +
								(VS.Fixed * vs[(d-1),s,y])
															
			}
		}
	}
		
	# OBSERVERS: Independent Intercepts
    # We estimate 12 separate intercepts (or however many groups you have).
    # No 'mean.prob', no 'beta.obs'. Just 'alpha'.
    
    for (o in 1:n.obs.fixed){
        # Each observer gets their own detection baseline.
        # dnorm(0, 1) covers the logit range comfortably.
        alpha[o] ~ dnorm(0, 1) 
    }
	    	
	# priors for Richards function parameters. 
	# Max is an AR(1) parameter 2026-02-06
	mu.log.Max ~ dnorm(7.6, 0.25) # Centered on ~2000
    rho.Max ~ dunif(-1, 1)
    sd.proc.Max ~ dnorm(0, 1)T(0,)
    tau.proc.Max <- pow(sd.proc.Max, -2)

    log.Max[1] ~ dnorm(mu.log.Max, tau.proc.Max)
    Max[1] <- exp(log.Max[1])
	
	# Peak for the first year:
	mu.P1 ~ dunif(30, 60)
	rho.P1 ~ dunif(-1,1)
	sd.proc.P1 ~ dnorm(0, 1)T(0,)
	tau.proc.P1 <- pow(sd.proc.P1, -2)
	
	P1[1] ~ dnorm(mu.P1, tau.proc.P1) #dunif(30, 60) #dgamma(P.alpha, P.beta) 	
	
	# Peak for the first year:
	mu.P2 ~ dunif(30, 60)
	rho.P2 ~ dunif(-1,1)
	sd.proc.P2 ~ dnorm(0, 1)T(0,)
	tau.proc.P2 <- pow(sd.proc.P2, -2)
	
	P2[1] ~ dnorm(mu.P2, tau.proc.P2) #dunif(30, 60) #dgamma(P.alpha, P.beta) 	
	
	for (y in 2:n.year){
		P1.mean[y] <- mu.P1 + rho.P1 * (P1[y-1] - mu.P1)
		P1[y] ~ dnorm(P1.mean[y], tau.proc.P1)
		
		P2.mean[y] <- mu.P2 + rho.P2 * (P2[y-1] - mu.P2)
		P2[y] ~ dnorm(P2.mean[y], tau.proc.P2)

		log.Max.mean[y] <- mu.log.Max + rho.Max * (log.Max[y-1] - mu.log.Max)
        log.Max[y] ~ dnorm(log.Max.mean[y], tau.proc.Max)
        Max[y] <- exp(log.Max[y]) 
	}
	
	# Uniform distributions were adjusted to capture entire posterior 
	# distributions of the hyper-parameters. 
	# Hyper parameters for S1 and S2 were changed following suggestions by Gemini 2026-02-03
	S1.alpha ~ dnorm(10, 0.1)T(0,)  #dunif(0.1, 50)
	S1.beta ~ dgamma(1, 1)   #dunif(0.01, 5)
	
	S2.alpha ~ dnorm(10, 0.1)T(0,)  #dunif(0.1, 50)
	S2.beta ~ dgamma(1, 1)     #dunif(0.01, 10)
	
	for (y in 1:n.year){
		S2[y] ~ dgamma(S1.alpha, S1.beta)  
	}
	
	S1 ~ dgamma(S2.alpha, S2.beta)  

	
	# K is fixed. 
	K <- 0.01  #~ dgamma(2, 1)   # or dlnorm(0, 1)
     
	## Beaufort and visibility	
	## Gemini: The precision parameters were changed from 0.001 to 0.25 2026-02-03
	BF.Fixed ~ dnorm(0,0.25) 
	VS.Fixed ~ dnorm(0,0.25) 
  
	### Summaries, Abundance Estimates, and Other Derived Quantities 
	for(t in 1:n.year){
		
		Raw.Est[t] <- sum(mean.N[1:n.days, t])
		# multiply raw estimates by correction factor for nighttime passage rates (below)
		Corrected.Est[t] <- Raw.Est[t] * corr.factor 
	}#t
  
	# Correction factor for nighttime passage rates (Perryman et al. 1999 Marine Mammal Science):
	corr.factor ~ dnorm(mean.corr, tau.corr)
	mean.corr <- 1.0875
	sd.corr <- 0.03625
	tau.corr <- pow(sd.corr,-2)
  		
	 
  # In the paper, Perryman et al. stated that "... the multiplicative correction 
  # factor for the entire visual survey based on a 15-h nocturnal (i.e., non-survey)
  # period would be 1 + 0.175f (SE = 0.116 * (14/24)), where f is the fraction of total
  # whales migrating afer 15 January." So, fixing the mean to be 1.0875, it assumes that
  # the median date of migration is Jan 15 (1 + 0.175 * 0.5 = 1.0875). This may have to
  # be fixed to adjust for the changing median migration date. SE of 0.03625 was not found
  # in the paper... 0.116*(14/24) = 0.0677	
  
  # There was a typographic error in Perryman et al. Laake et al. (2012) stated that "... a
  # standard error of f x 15/24 x 0.116 after correcting the typographic errors in Perryman
  # et al. (1999).
}
